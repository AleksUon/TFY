Курсовая по Теории формальных языков

Было разработано две версии программы. Одна включает в себя GUI, вторая без GUI и ввод происходи в терминале. Оба кода имеют одинаковые функции и работоспособность.

7 вариант 12311


**1. Операции языка**
   
  1.1. Операции группы «отношение»
  
    <операции_группы_отношения>::= <> | = | < | <= | > | >=
    
  1.2. Операции группы «сложение»
  
    <операции_группы_сложения>::= + | - | or
    
  1.3. Операции группы «умножение»
  
    <операции_группы_умножения>::= * | / | and
    
  1.4. Унарная операция
  
    <унарная_операция>::= not


**2. Правила, определяющие структуру программы**
   
  2.1. Структура программы
    
    <программа>::= «{» {/ (<описание> | <оператор>) ; /} «}»


**3. Правила, определяющие раздел описания переменных**
   
  3.1. Синтаксис команд описания данных
    
    <описание>::= <тип> <идентификатор> { , <идентификатор> }


**4. Правила, определяющие типы данных**
   
  4.1. Описание типов данных
  
    <тип>::= % | ! | $


**5. Правило, определяющее оператор программы**
   
<оператор>::= <составной> | <присваивания> | <условный> | <фиксированного_цикла> | <условного_цикла> | <ввода> | <вывода>

  5.1. Синтаксис составного оператора
  
    <составной>::= «[» <оператор> { ( : | перевод строки) <оператор> } «]»
    
  5.2. Синтаксис оператора присваивания
  
    <присваивания>::= <идентификатор> as <выражение>
    
  5.3. Синтаксис оператора условного перехода
  
    <условный>::= if <выражение> then <оператор> [ else <оператор>]
    
  5.4. Синтаксис оператора цикла с фиксированным числом повторений
  
    <фиксированного_цикла>::= for <присваивания> to <выражение> do <оператор>
    
  5.5. Синтаксис условного оператора цикла
  
    <условного_цикла>::= while <выражение> do <оператор>
    
  5.6. Синтаксис оператора ввода
  
    <ввода>::= read «(»<идентификатор> {, <идентификатор> } «)»
    
  5.7. Синтаксис оператора вывода
  
    <вывода>::= write «(»<выражение> {, <выражение> } «)»


**6. Многострочные комментарии в программе**

      { … }


**Правила языка для всех вариантов:**

  Выражения языка задаются правилами:
  
    1. <выражение>::= <операнд>{<операции_группы_отношения> <операнд>}
    2. <операнд>::= <слагаемое> {<операции_группы_сложения> <слагаемое>}
    3. <слагаемое>::= <множитель> {<операции_группы_умножения> <множитель>}
    4. <множитель>::= <идентификатор> | <число> | <логическая_константа> | <унарная_операция> <множитель> | «(»<выражение>«)»
    5. <число>::= <целое> | <действительное>
    6. <логическая_константа>::= true | false

  Правила, определяющие идентификатор, букву и цифру:
  
    7. <идентификатор>::= <буква> {<буква> | <цифра>}
    8. <буква>::= A | B | C | D | E | F | G | H | I | J | K | L | M | N | O | P | Q | R | S | T | U | V | W | X | Y | Z | a | b | c | d | e | f | g | h | i | j | k | l | m | n | o | p | q | r | s | t | u | v | w | x | y | z
    9. <цифра>::= 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
  
  Правила, определяющие целые числа:
  
    10. <целое>::= <двоичное> | <восьмеричное> | <десятичное> | <шестнадцатеричное>
    11. <двоичное>::= {/ 0 | 1 /} (B | b)
    12. <восьмеричное>::= {/ 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 /} (O | o)
    13. <десятичное>::= {/ <цифра> /} [D | d]
    14. <шестнадцатеричное>::= <цифра> {<цифра> | A | B | C | D | E | F | a | b | c | d | e | f} (H | h)
  
  Правила, описывающие действительные числа:
  
    15. <действительное>::= <числовая_строка> <порядок> | [<числовая_строка>] . <числовая_строка> [порядок]
    16. <числовая_строка>::= {/ <цифра> /}
    17. <порядок>::= ( E | e )[+ | -] <числовая_строка>
